(import "env" "wavm_halt_and_set_finished" (func $wavm_halt_and_set_finished))

(func $main (local f32)
	;; abs, neg
	(f32.const -1)
	(f32.abs)
	(call $assert_f32 (f32.const 1))
	(f32.neg)
	(call $assert_f32 (f32.const -1))
	(drop)

	;; ceil
	(f32.const -0.8)
	(f32.ceil)
	(call $assert_f32 (f32.const -0))
	(f32.ceil)
	(call $assert_f32 (f32.const -0))
	(drop)

	;; floor
	(f32.const -0.8)
	(f32.floor)
	(call $assert_f32 (f32.const -1))
	(f32.floor)
	(call $assert_f32 (f32.const -1))
	(drop)

	;; trunc
	(f32.const -0.8)
	(f32.trunc)
	(call $assert_f32 (f32.const -0))
	(f32.trunc)
	(call $assert_f32 (f32.const -0))
	(drop)

	;; nearest
	(f32.const -0.8)
	(f32.nearest)
	(call $assert_f32 (f32.const -1))
	(f32.nearest)
	(call $assert_f32 (f32.const -1))
	(drop)

	;; sqrt
	(f32.const 123.456)
	(f32.sqrt)
	(call $assert_f32 (f32.const 11.111075))
	(f32.sqrt)
	(call $assert_f32 (f32.const 3.333328))
	(drop)

	;; add, sub, mul, div
	(f32.const 1.5)
	(f32.const 2)
	(f32.add)
	(call $assert_f32 (f32.const 3.5))
	(f32.const 5)
	(f32.sub)
	(call $assert_f32 (f32.const -1.5))
	(f32.const 100)
	(f32.mul)
	(call $assert_f32 (f32.const -150))
	(f32.const 7)
	(f32.div)
	(call $assert_f32 (f32.const -21.428572))
	(drop)

	;; min, max, copysign
	(f32.const 5)
	(f32.const -2)
	(f32.min)
	(call $assert_f32 (f32.const -2))
	(f32.const -5)
	(f32.max)
	(call $assert_f32 (f32.const -2))
	(f32.const 1)
	(f32.copysign)
	(call $assert_f32 (f32.const 2))
	(drop)

	;; eq
	(f32.const 5)
	(f32.const 5)
	(f32.eq)
	(call $assert_true)
	(f32.const -5)
	(f32.const 5)
	(f32.eq)
	(call $assert_false)
	(f32.const 1)
	(f32.const 0)
	(f32.div)
	(local.tee 0)
	(local.get 0)
	(f32.sub)
	(local.tee 0)
	(local.get 0)
	(f32.eq)
	(call $assert_false)

	;; ne
	(local.get 0)
	(local.get 0)
	(f32.ne)
	(call $assert_true)
	(f32.const 1)
	(f32.const 1)
	(f32.ne)
	(call $assert_false)
	(f32.const 1)
	(f32.const 2)
	(f32.ne)
	(call $assert_true)

	;; lt
	(f32.const 1)
	(f32.const 2)
	(f32.lt)
	(call $assert_true)
	(f32.const 2)
	(f32.const 2)
	(f32.lt)
	(call $assert_false)
	(f32.const 3)
	(f32.const 2)
	(f32.lt)
	(call $assert_false)

	;; gt
	(f32.const 1)
	(f32.const 2)
	(f32.gt)
	(call $assert_false)
	(f32.const 2)
	(f32.const 2)
	(f32.gt)
	(call $assert_false)
	(f32.const 3)
	(f32.const 2)
	(f32.gt)
	(call $assert_true)

	;; le
	(f32.const 1)
	(f32.const 2)
	(f32.le)
	(call $assert_true)
	(f32.const 2)
	(f32.const 2)
	(f32.le)
	(call $assert_true)
	(f32.const 3)
	(f32.const 2)
	(f32.le)
	(call $assert_false)

	;; ge
	(f32.const 1)
	(f32.const 2)
	(f32.ge)
	(call $assert_false)
	(f32.const 2)
	(f32.const 2)
	(f32.ge)
	(call $assert_true)
	(f32.const 3)
	(f32.const 2)
	(f32.ge)
	(call $assert_true)

	;; f32 -> i32 truncation
	(f32.const -2.5)
	(i32.trunc_sat_f32_s)
	(call $assert_i32 (i32.const -2))
	(f32.const -2.5)
	(i32.trunc_sat_f32_u)
	(call $assert_i32 (i32.const 0))
	(f32.const 1000000000000)
	(i32.trunc_sat_f32_u)
	(i32.const -1)
	(call $assert_i32)
	(f32.const 1000000000000)
	(i32.trunc_sat_f32_s)
	(i32.const 1)
	(i32.shl (i32.const 31))
	(i32.sub (i32.const 1))
	(call $assert_i32)
	(f32.const 1000000000000)
	(i32.trunc_sat_f32_s)
	(i32.gt_s (i32.const 0))
	(call $assert_true)
	(f32.const -1000000000000)
	(i32.trunc_sat_f32_s)
	(i32.const 1)
	(i32.shl (i32.const 31))
	(call $assert_i32)
	(f32.const -1000000000000)
	(i32.trunc_sat_f32_s)
	(i32.lt_s (i32.const 0))
	(call $assert_true)

	;; f32 -> i64 truncation
	(f32.const -2.5)
	(i64.trunc_sat_f32_s)
	(call $assert_i64 (i64.const -2))
	(f32.const -2.5)
	(i64.trunc_sat_f32_u)
	(call $assert_i64 (i64.const 0))
	(f32.const 1000000000000000000000)
	(i64.trunc_sat_f32_u)
	(i64.const -1)
	(call $assert_i64)
	(f32.const 1000000000000000000000)
	(i64.trunc_sat_f32_s)
	(i64.const 1)
	(i64.shl (i64.const 63))
	(i64.sub (i64.const 1))
	(call $assert_i64)
	(f32.const 1000000000000000000000)
	(i64.trunc_sat_f32_s)
	(i64.gt_s (i64.const 0))
	(call $assert_true)
	(f32.const -1000000000000000000000)
	(i64.trunc_sat_f32_s)
	(i64.const 1)
	(i64.shl (i64.const 63))
	(call $assert_i64)
	(f32.const -1000000000000000000000)
	(i64.trunc_sat_f32_s)
	(i64.lt_s (i64.const 0))
	(call $assert_true)

	(call $wavm_halt_and_set_finished)
)

(func $assert_f32 (param f32 f32) (result f32)
	(local.get 0)
	(i32.reinterpret_f32)
	(local.get 1)
	(i32.reinterpret_f32)
	(i32.sub)
	(local.get 0)
	(local.get 1)
	(f32.ne)
	(i32.or)
	(if
		(then
			;; push the params back on the stack for debugging
			(local.get 0)
			(local.get 1)
			unreachable
		)
	)
	(local.get 0)
)

(func $assert_i32 (param i32 i32)
	(local.get 0)
	(local.get 1)
	(i32.sub)
	(if
		(then
			;; push the params back on the stack for debugging
			(local.get 0)
			(local.get 1)
			unreachable
		)
	)
)

(func $assert_i64 (param i64 i64)
	(local.get 0)
	(local.get 1)
	(i64.sub)
	(i64.eqz)
	(i32.eqz)
	(if
		(then
			;; push the params back on the stack for debugging
			(local.get 0)
			(local.get 1)
			unreachable
		)
	)
)

(func $assert_true (param i32)
	(local.get 0)
	(i32.eqz)
	(if
		(then
			(local.get 0)
			unreachable
		)
	)
)

(func $assert_false (param i32)
	(local.get 0)
	(if
		(then
			(local.get 0)
			unreachable
		)
	)
)

(start $main)
